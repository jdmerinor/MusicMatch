// Block definition (there may be more than one block per file)
FUNCTION_BLOCK recomendador

// Definicion de las variables de entrada
VAR_INPUT
    ritmo : REAL;
    letra : REAL;
    momento_adecuado : REAL;
END_VAR

// Definicion de las variables de salida
VAR_OUTPUT
    evaluacion : REAL;
END_VAR

// Fuzificar variable de entrada 'ritmo' (De 0 a 10)
FUZZIFY ritmo
    TERM malo    := (0, 1) (1,1) (3.5,0); 
    TERM regular := (1.5, 0) (3,1) (6,1) (7.5,0);
    TERM bueno   := (5.5, 0) (8, 1) (10,1);
END_FUZZIFY

// Fuzificar variable de entrada 'letra' (De 0 a 10)
FUZZIFY letra
    TERM superficial := (0, 1) (3, 1) (7,0) ;
    TERM profunda    := (3,0) (7,1) (10,1);
END_FUZZIFY

// Fuzificar variable de entrada 'momento_adecuado' (De 0 a 10)
FUZZIFY momento_adecuado
    TERM poco_adecuado :=  (0, 1) (1,1) (4, 0); 
    TERM indiferente   :=  (1.5, 0) (3.5,1) (5.5,1) (7.5,0);
    TERM muy_adecuado  :=  (5, 0) (8, 1) (10,1);
END_FUZZIFY

// Desfuzificar variable de salida 'evaluacion' (De 1 a 5 estrellas)
DEFUZZIFY evaluacion
    TERM mala    :=  (1,1)  (2.5,0);
    TERM regular :=  (2,0) (3,1) (4,0);
    TERM buena   :=  (3.5,0) (5,1);

    // Usar metodo de desfuzificacion 'Center Of Gravity'
    METHOD : COG;

    // El valor por defecto es 0 (Si ninguna regla activa el desfuzificador)
    DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
    // Use 'min' for 'and' (also implicit use 'max')
    // for 'or' to fulfill DeMorgan's Law)
    AND : MIN;
    // Use 'min' activation method
    ACT : MIN;
    // Use 'max' accumulation method
    ACCU : MAX;

    RULE 1 : IF ritmo IS malo AND letra IS superficial AND momento_adecuado IS poco_adecuado
                THEN evaluacion IS mala;

    RULE 2 : IF ritmo IS bueno AND letra IS profunda AND momento_adecuado IS muy_adecuado
                THEN evaluacion IS buena; 

    RULE 3 : IF ritmo IS malo OR letra IS superficial OR momento_adecuado IS poco_adecuado
                THEN evaluacion IS mala; 

    RULE 4 : IF ritmo IS bueno OR letra IS profunda OR momento_adecuado IS muy_adecuado
                THEN evaluacion IS buena;

    RULE 5 : IF ritmo IS regular OR momento_adecuado IS indiferente
                THEN evaluacion IS regular;
    
END_RULEBLOCK

END_FUNCTION_BLOCK
